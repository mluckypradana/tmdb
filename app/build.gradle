apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "kotlin-kapt"

final buildDate = new Date().format("yyyyMMddHHmm")
android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.luc.base"
        minSdkVersion 21
        targetSdkVersion 29
        multiDexEnabled true
        versionCode buildDate.toBigInteger()
        versionName buildDate
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    dataBinding {
        enabled = true
    }

    flavorDimensions "main"
    productFlavors {
        mock {
            dimension "main"
            externalNativeBuild {
                cmake {
                    cppFlags '-DMOCK'
                    arguments "-DANDROID_STL=c++_shared"
                }
            }
        }
        dev {
            dimension "main"
            externalNativeBuild {
                cmake {
                    cppFlags '-DDEV'
                    arguments "-DANDROID_STL=c++_shared"
                }
            }
        }
        prod {
            dimension "main"
            externalNativeBuild {
                cmake {
                    cppFlags '-DPROD'
                    arguments "-DANDROID_STL=c++_shared"
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                output -> outputFileName = "app_${variant.name}_${variant.versionName}.apk"
            }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    sourceSets {
        test {
            resources.srcDirs += ['sampledata']
        }
        androidTest {
            resources.srcDirs += ['sampledata']
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.navigation:navigation-fragment:2.2.2'
    implementation 'androidx.navigation:navigation-ui:2.2.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.13'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.koin:koin-test:2.0.1'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test:core:1.2.0"
    testImplementation 'org.mockito:mockito-core:2.25.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    //Koin
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-core-ext:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    implementation "com.android.support:multidex:1.1.0"
    //Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:$androidxArchVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    // Google
    implementation "com.google.code.gson:gson:$gson_version"
    //SessionStorage
    implementation 'com.orhanobut:hawk:2.0.1'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
}
